; Virtual AC3 Encoder installer (Inno Setup 6). Per-user install, no admin required.
; Build:  ISCC.exe /DAppVersion=0.1.0 /DSourceDir=<dist> /O. installer\virtual-ac3-encoder.iss
#ifndef AppVersion
  #define AppVersion "0.0.0-dev"
#endif
#ifndef SourceDir
  #define SourceDir "..\dist"
#endif

[Setup]
AppId={{B3D7F2A1-6C84-4E59-9A0F-2D7C1E8B4A60}
AppName=Virtual AC3 Encoder
AppVersion={#AppVersion}
AppPublisher=strepto42
AppPublisherURL=https://github.com/strepto42/virtual-ac3-encoder
AppSupportURL=https://github.com/strepto42/virtual-ac3-encoder/issues
DefaultDirName={localappdata}\Virtual AC3 Encoder
DefaultGroupName=Virtual AC3 Encoder
PrivilegesRequired=lowest
DisableProgramGroupPage=yes
WizardStyle=modern
Compression=lzma2
SolidCompression=yes
OutputBaseFilename=virtual-ac3-encoder-setup-{#AppVersion}
UninstallDisplayName=Virtual AC3 Encoder {#AppVersion}

[Files]
Source: "{#SourceDir}\engine.exe";        DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\*.dll";             DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\supervisor.vbs";    DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\stop-engine.ps1";   DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\QUICKSTART.txt";    DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\README.md";         DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\LICENSE";           DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\NOTICE.md";         DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourceDir}\scripts\*";         DestDir: "{app}\scripts"; Flags: ignoreversion recursesubdirs
; Don't overwrite the user's settings on upgrade:
Source: "{#SourceDir}\virtual-ac3-encoder.conf"; DestDir: "{app}"; Flags: onlyifdoesntexist

[Icons]
; Autostart hidden at logon via the self-locating supervisor:
Name: "{userstartup}\Virtual AC3 Encoder"; Filename: "{sys}\wscript.exe"; Parameters: """{app}\supervisor.vbs"""; WorkingDir: "{app}"
; Start Menu:
Name: "{group}\Edit config";        Filename: "{win}\notepad.exe"; Parameters: """{app}\virtual-ac3-encoder.conf"""
Name: "{group}\View log";           Filename: "{win}\notepad.exe"; Parameters: """{app}\engine.log"""
Name: "{group}\List audio devices"; Filename: "{cmd}"; Parameters: "/k """"{app}\engine.exe"""" --list"
Name: "{group}\Quick start";        Filename: "{app}\QUICKSTART.txt"
Name: "{group}\Get VB-CABLE (required virtual cable)"; Filename: "https://vb-audio.com/Cable/"
Name: "{group}\Project page";       Filename: "https://github.com/strepto42/virtual-ac3-encoder"
Name: "{group}\Uninstall Virtual AC3 Encoder"; Filename: "{uninstallexe}"

[Run]
; Start it now (hidden); then offer the quick start (VB-CABLE setup).
Filename: "{sys}\wscript.exe"; Parameters: """{app}\supervisor.vbs"""; WorkingDir: "{app}"; Flags: nowait runhidden
Filename: "{app}\QUICKSTART.txt"; Description: "Open the quick start (VB-CABLE setup)"; Flags: postinstall shellexec skipifsilent

[UninstallRun]
; Stop the running engine + supervisor before files are removed.
Filename: "powershell.exe"; Parameters: "-NoProfile -ExecutionPolicy Bypass -File ""{app}\stop-engine.ps1"""; Flags: runhidden; RunOnceId: "StopEngine"

[UninstallDelete]
Type: files; Name: "{app}\engine.log"
